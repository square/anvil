import org.gradle.util.internal.VersionNumber

ext {
  ci = (System.getenv('CI') ?: 'false').toBoolean()

  autoServiceVersion = '1.0.1'
  daggerVersion = '2.39.1'
  espressoVersion = '3.2.0'

  agpVersion = project.hasProperty('square.agpVersion') ?
      project.getProperty('square.agpVersion') : '4.2.2'

  kotlinVersion = project.hasProperty('square.kotlinVersion') ?
      project.getProperty('square.kotlinVersion') : '1.6.10'

  kotlinUseIR = (project.hasProperty('square.useIR') ?
      project.getProperty('square.useIR') : 'true').toBoolean()

  // TODO: remove this property with Kotlin 1.6.20.
  kotlinPreview = !kotlinVersion.startsWith('1.6.1')

  generateDaggerFactoriesWithAnvil = (project.hasProperty('square.generateDaggerFactoriesWithAnvil') ?
      project.getProperty('square.generateDaggerFactoriesWithAnvil') : 'true').toBoolean()

  // Used to shorten the time it takes to run :compiler:test. On my Mac Pro this reduces the
  // runtime from 2+min to 1min. We don't need to run all tests for sanity checks. If the flag is
  // false, then we run each test with one annotation instead of all options. We also skip tests
  // that run the Dagger annotation processor (KAPT is slow).
  //
  // This flag should only be used for local development.
  fullTestRun = (project.hasProperty('square.fullTestRun') ?
      project.getProperty('square.fullTestRun') : 'true').toBoolean()

  if (ci && !fullTestRun) {
    throw GradleException("Don't disable fullTestRun in CI!")
  }

  // TODO: Remove when we remove the old backend.
  // TODO Repeatable: After cleaning up all deprecated methods remove the `&& false`.
  makeWarningsErrors = ci && (kotlinVersion.startsWith('1.5') || kotlinUseIR) && false

  ktlintVersion = "0.41.0"

  deps = [
      android_gradle_plugin: "com.android.tools.build:gradle:$agpVersion",
      anvil_plugin: "com.squareup.anvil:gradle-plugin:${findProperty('VERSION_NAME')}",
      maven_publishing_plugin: "com.vanniktech:gradle-maven-publish-plugin:0.18.0",
      gradle_publishing_plugin: "com.gradle.publish:plugin-publish-plugin:0.15.0",
      ktlint_plugin: "org.jlleitschuh.gradle:ktlint-gradle:10.2.0",

      androidx: [
          appcompat: "androidx.appcompat:appcompat:1.1.0",
          core: "androidx.core:core-ktx:1.3.0",

          test: [
              core: "androidx.test:core:1.1.0",

              espresso: [
                  core: "androidx.test.espresso:espresso-core:$espressoVersion",
              ],

              junit: "androidx.test.ext:junit:1.1.1",
              rules: "androidx.test:rules:1.1.0",
              runner: "androidx.test:runner:1.1.0",
          ],

          material: "com.google.android.material:material:1.1.0",
      ],

      auto: [
          service: [
              annotations: "com.google.auto.service:auto-service-annotations:$autoServiceVersion",
              processor: "com.google.auto.service:auto-service:$autoServiceVersion"
          ]
      ],

      dagger2: [
          dagger: "com.google.dagger:dagger:$daggerVersion",
          compiler: "com.google.dagger:dagger-compiler:$daggerVersion"
      ],

      inject: "javax.inject:javax.inject:1",
      jsr250: "javax.annotation:jsr250-api:1.0",
      junit: "junit:junit:4.13",

      kotlin: [
          annotation_processing_embeddable: "org.jetbrains.kotlin:kotlin-annotation-processing-embeddable:$kotlinVersion",
          compile_testing: "com.github.tschuchortdev:kotlin-compile-testing:${kotlinPreview ? '1.4.8-20220210.154948-5' : '1.4.7'}",
          compiler: "org.jetbrains.kotlin:kotlin-compiler-embeddable:$kotlinVersion",
          dokka: "org.jetbrains.dokka:dokka-gradle-plugin:1.6.10",
          gradle_plugin: "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion",
          gradle_plugin_api: "org.jetbrains.kotlin:kotlin-gradle-plugin-api:$kotlinVersion",
          metadata: "org.jetbrains.kotlinx:kotlinx-metadata-jvm:0.3.0",
          reflect: "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion",
          test: "org.jetbrains.kotlin:kotlin-test-junit:$kotlinVersion",
      ],

      kotlinpoet: "com.squareup:kotlinpoet:1.10.1",

      truth: "com.google.truth:truth:1.1.3",
  ]
}
