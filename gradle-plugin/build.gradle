buildscript {
  apply from: rootProject.file('../dependencies.gradle')

  repositories {
    google()
    gradlePluginPortal()
    mavenCentral()
    jcenter()
    maven {
      url "https://dl.bintray.com/kotlin/kotlin-eap"
    }
    maven {
      url "http://dl.bintray.com/kotlin/kotlin-dev"
    }
  }

  dependencies {
    classpath deps.kotlin_gradle.gradle_plugin
    classpath deps.maven_publishing_plugin
    classpath deps.gradle_publishing_plugin
    classpath deps.ktlint_plugin
  }
}

repositories {
  google()
  mavenCentral()
  jcenter()
  maven {
    url "https://dl.bintray.com/kotlin/kotlin-eap"
  }
  maven {
    url "http://dl.bintray.com/kotlin/kotlin-dev"
  }
}

apply from: rootProject.file('copy_properties.gradle')

apply plugin: 'java-gradle-plugin'
apply plugin: 'org.jetbrains.kotlin.jvm'
apply plugin: 'org.jetbrains.kotlin.kapt'
apply plugin: 'com.vanniktech.maven.publish'
apply plugin: 'com.gradle.plugin-publish'
apply plugin: 'org.jlleitschuh.gradle.ktlint'

apply from: rootProject.file('generate_build_properties.gradle')

gradlePlugin {
  plugins {
    anvilPlugin {
      id = "com.squareup.anvil"
      displayName = project.findProperty('POM_NAME')
      implementationClass = "com.squareup.anvil.plugin.AnvilPlugin"
      description = project.findProperty('POM_DESCRIPTION')
    }
  }
}

pluginBundle {
  website = project.findProperty('POM_URL')
  vcsUrl = project.findProperty('POM_SCM_URL')
  tags = ['dagger2', 'dagger2-android', 'kotlin', 'kotlin-compiler-plugin']
}

tasks.withType(JavaCompile).configureEach {
  sourceCompatibility = JavaVersion.VERSION_1_8
  targetCompatibility = JavaVersion.VERSION_1_8
}

//noinspection UnnecessaryQualifiedReference
tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
  kotlinOptions {
    jvmTarget = JavaVersion.VERSION_1_8
    freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
  }
}

dependencies {
  implementation deps.kotlin_gradle.gradle_plugin
  implementation deps.kotlin_gradle.gradle_plugin_api
  implementation deps.kotlin_gradle.stdlib

  // Compile only so that Java / Kotlin modules don't download it unnecessarily.
  compileOnly deps.android_gradle_plugin

  compileOnly deps.auto.service.annotations

  kapt deps.auto.service.processor
}
