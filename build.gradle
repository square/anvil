buildscript {
  apply from: rootProject.file('dependencies.gradle')

  repositories {
    google()
    gradlePluginPortal()
    mavenCentral()
    jcenter()
    maven {
      url "https://dl.bintray.com/kotlin/kotlin-eap"
    }
    maven {
      url "http://dl.bintray.com/kotlin/kotlin-dev"
    }
  }

  dependencies {
    classpath deps.android_gradle_plugin
    classpath deps.kotlin.gradle_plugin
    classpath deps.ktlint_plugin
    classpath deps.anvil_plugin
    classpath deps.maven_publishing_plugin
  }
}

println "Versions: " + [
    "Kotlin": ext.kotlinVersion,
    "Gradle": gradle.gradleVersion,
    "Kotlin use IR": ext.kotlinUseIR,
    "Kotlin IR supported": ext.kotlinIrSupported,
]

allprojects {
  repositories {
    google()
    mavenCentral()
    jcenter()
    maven {
      url "https://dl.bintray.com/kotlin/kotlin-eap"
    }
    maven {
      url "http://dl.bintray.com/kotlin/kotlin-dev"
    }
  }
}

subprojects {
  apply plugin: 'org.jlleitschuh.gradle.ktlint'

  tasks.withType(Test).configureEach {
    testLogging {
      events 'passed', 'failed', 'skipped', 'standardOut', 'standardError'
      exceptionFormat 'FULL'
      showCauses true
      showExceptions true
      showStackTraces true
      showStandardStreams true
    }
  }

  tasks.withType(JavaCompile).configureEach {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
  }

  //noinspection UnnecessaryQualifiedReference
  tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
      jvmTarget = JavaVersion.VERSION_1_8
      useIR = rootProject.ext.kotlinUseIR
      allWarningsAsErrors = true
    }
  }

  // TODO: fix obscure dependency substitution error
  // This is the ideal solution, but doesn't quite work for some reason. The workaround below is
  // needed to make it work for now.
  configurations.all {
    resolutionStrategy.dependencySubstitution {
      substitute module("com.squareup.anvil:annotations") with project(':annotations')
      substitute module("com.squareup.anvil:compiler") with project(':compiler')
    }
  }
  // This is an ugly workaround.
  def brokenProjects = [
      project(':integration-tests:library'),
      project(':integration-tests:tests'),
      project(':sample:library'),
      project(':sample:app')
  ]
  brokenProjects.each { brokenProject ->
    brokenProject.configurations.whenObjectAdded {
      if (it.name == 'api') {
        brokenProject.dependencies.add('api', project(':annotations'))
      }
      if (it.name == 'kotlinCompilerPluginClasspath') {
        brokenProject.dependencies.add('kotlinCompilerPluginClasspath', project(':compiler'))
      }
    }
  }
  // End ugly workaround.
}

task clean(type: Delete) {
  delete rootProject.buildDir
}