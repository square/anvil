buildscript {
  apply from: rootProject.file('dependencies.gradle')

  repositories {
    google()
    gradlePluginPortal()
    mavenCentral()
    maven { url 'https://maven.pkg.jetbrains.space/kotlin/p/kotlin/dev' }
  }

  dependencies {
    classpath deps.android_gradle_plugin
    classpath deps.kotlin.dokka
    classpath deps.kotlin.gradle_plugin
    classpath deps.ktlint_plugin
    classpath deps.anvil_plugin
    classpath deps.maven_publishing_plugin
  }
}

println "Versions: " + [
    "Kotlin": ext.kotlinVersion,
    "Gradle": gradle.gradleVersion,
    "Kotlin use IR in tests": ext.kotlinUseIR
]

allprojects {
  repositories {
    google()
    mavenCentral()
    if (rootProject.ext.kotlinPreview) {
      maven { url 'https://maven.pkg.jetbrains.space/kotlin/p/kotlin/dev' }
      maven { url 'https://maven.pkg.jetbrains.space/kotlin/p/kotlin/bootstrap' }
      maven { url 'https://s01.oss.sonatype.org/content/repositories/snapshots' }
      maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    }
  }
}

boolean configureOnDemandEnabled = getProperty("org.gradle.configureondemand", "false").toBoolean()

subprojects {
  if (buildFile.exists()) {
    apply plugin: 'org.jlleitschuh.gradle.ktlint'

    ktlint {
      version = rootProject.ext.ktlintVersion
      verbose = true
    }
  }

  tasks.withType(Test).configureEach {
    testLogging {
      events 'passed', 'failed', 'skipped', 'standardOut', 'standardError'
      exceptionFormat 'FULL'
      showCauses true
      showExceptions true
      showStackTraces true
      showStandardStreams true
    }
  }

  pluginManager.withPlugin("java") {
    // Use JDK 11 but target Java 8 for maximum compatibility.
    java {
      toolchain {
        languageVersion.set(JavaLanguageVersion.of(11))
      }
    }

    tasks.withType(JavaCompile).configureEach {
      options.release.set(8)
    }
  }

  //noinspection UnnecessaryQualifiedReference
  tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
      jvmTarget = JavaVersion.VERSION_1_8

      allWarningsAsErrors = rootProject.ext.makeWarningsErrors
      freeCompilerArgs += ["-opt-in=kotlin.RequiresOptIn"]

      boolean optInExperimental = project.name != "annotations"
          && project.name != "scopes"
          && project.name != "dagger-factories-only"
      if (optInExperimental) {
        freeCompilerArgs += "-opt-in=com.squareup.anvil.annotations.ExperimentalAnvilApi"
      }
    }
  }

  configurations.all {
    resolutionStrategy.dependencySubstitution {
      substitute module("com.squareup.anvil:annotations") with project(':annotations')
      substitute module("com.squareup.anvil:compiler") with project(':compiler')
      substitute module("com.squareup.anvil:compiler-api") with project(':compiler-api')
      substitute module("com.squareup.anvil:compiler-utils") with project(':compiler-utils')
    }

    resolutionStrategy {
      force deps.kotlin.metadata
    }
  }

  // We disable configure on demand in the local gradle.properties, but you can override this flag
  // either in the CLI command or in ~/.gradle/gradle.properties. We really need to disable
  // configure on demand in order to support the dependency substitution feature above. As a
  // workaround force the evaluation of subprojects.
  //
  // The other option would be to stop the build if we detect configure on the demand, but some
  // folks (including myself ðŸ™ƒ) set the flag globally in the Gradle home dir. The workaround
  // is good enough for now.
  if (configureOnDemandEnabled) {
    //noinspection UnnecessaryQualifiedReference
    (it as org.gradle.api.internal.project.DefaultProject).evaluate()
  }
}

task clean(type: Delete) {
  delete rootProject.buildDir
}

def getProperty(String name, String defaultValue) {
  return project.hasProperty(name) ? project.getProperty(name) : defaultValue
}

// Register the task that can create the benchmark project.
//noinspection UnnecessaryQualifiedReference
tasks.register("createBenchmarkProject", com.squareup.anvil.benchmark.CreateBenchmarkProjectTask)
