pluginManagement {
  repositories {
    google()
    gradlePluginPortal()
    mavenCentral()
  }
}

dependencyResolutionManagement {
  versionCatalogs {
    Map<String, String> overrides = new LinkedHashMap()
    System.getProperties().forEach { key, value ->
      String keyString = key.toString()
      if (keyString.startsWith("override_")) {
        overrides.put(keyString, value.toString())
        if (keyString.endsWith("_kotlin")) {
          // TODO hardcoded to match what's in libs.versions.toml, but kinda ugly
          overrides.put("override_ksp", "$value-1.0.11")
        }
      }
    }
    libs {
      for (Map.Entry<String, String> entry : overrides.entrySet()) {
        String key = entry.getKey()
        String value = entry.getValue()
        String catalogKey = key.substring("override_".length())
        println("Overriding $catalogKey with $value")
        version(catalogKey, value)
      }
    }
  }

  repositories {
    google()
    mavenCentral()
    if (hasProperty("anvil.allowSnapshots")) {
      maven { url 'https://maven.pkg.jetbrains.space/kotlin/p/kotlin/dev' }
      maven { url 'https://maven.pkg.jetbrains.space/kotlin/p/kotlin/bootstrap' }
      maven { url 'https://s01.oss.sonatype.org/content/repositories/snapshots' }
      maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    }
  }
}

include ':annotations'
include ':compiler'
include ':compiler-api'
include ':compiler-utils'
include ':integration-tests:code-generator'
include ':integration-tests:code-generator-tests'
include ':integration-tests:dagger-factories-only'
include ':integration-tests:library'
include ':integration-tests:mpp:android-module'
include ':integration-tests:mpp:jvm-module'
include ':integration-tests:tests'
include ':sample:app'
include ':sample:library'
include ':sample:scopes'

// Use the local version instead of a remote artifact.
includeBuild('gradle-plugin') {
  dependencySubstitution {
    substitute module('com.squareup.anvil:gradle-plugin') using project(':')
  }
}

// Include the benchmark modules if they exist.
def benchmarkSettingsFile = file('benchmark/settings.gradle')
if (benchmarkSettingsFile.exists()) {
  apply from: benchmarkSettingsFile
}
