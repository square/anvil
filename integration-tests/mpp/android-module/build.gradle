import com.android.build.gradle.api.UnitTestVariant
import com.android.builder.core.BuilderConstants
import com.squareup.anvil.plugin.AndroidVariantFilter

plugins {
  alias(libs.plugins.kotlin.multiplatform)
  alias(libs.plugins.kotlin.kapt)
  alias(libs.plugins.agp.library)
  id("com.squareup.anvil")
}

android {
  compileSdk = 33
  namespace = "com.squareup.anvil.mpp"

  defaultConfig {
    minSdk = 24
    targetSdk = 33
    versionCode = 1
    versionName = "1.0.0"
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_11
    targetCompatibility JavaVersion.VERSION_11
  }

  lint {
    it.warningsAsErrors = true
  }

  variantFilter { variant ->
    if (variant.buildType.name == BuilderConstants.RELEASE) {
      variant.setIgnore(true)
    }
  }
}

kotlin {

  // The `android()` target is deprecated in Kotlin 1.9.0+,
  // but the new `androidTarget()` alternative doesn't exist in KGP 1.8.x.
  if (kotlin.coreLibrariesVersion < "1.9.0")
    android()
  else {
    androidTarget()
  }

  sourceSets {
    androidMain {
      dependencies {
        implementation libs.dagger2
      }
    }

    androidUnitTest {
      dependencies {
        implementation libs.junit
        implementation libs.truth
      }
    }
  }
}

anvil {
  variantFilter { filter ->
    if (filter instanceof AndroidVariantFilter &&
        filter.androidVariant instanceof UnitTestVariant) {
      ignore = true
    }
  }
}

dependencies {
  kapt libs.dagger2.compiler

  // This dependency isn't needed. It's only here for testing purposes (this is still an
  // integration test).
  anvilAndroidTest project(':integration-tests:code-generator')
}
