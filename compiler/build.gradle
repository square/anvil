plugins {
  alias(libs.plugins.kotlin.jvm)
  alias(libs.plugins.kotlin.kapt)
  alias(libs.plugins.mavenPublish)
}

apply from: project.file('generate_build_properties.gradle')

dependencies {
  implementation project(':annotations')
  implementation project(':compiler-api')
  implementation project(':compiler-utils')
  implementation libs.dagger2
  implementation libs.jsr250
  implementation libs.kotlinpoet

  compileOnly libs.auto.service.annotations
  compileOnly libs.kotlin.compiler

  kapt libs.auto.service.processor

  testImplementation testFixtures(project(":compiler-utils"))
  testImplementation libs.dagger2.compiler
  testImplementation libs.kotlin.annotationProcessingEmbeddable
  testImplementation libs.kotlin.compileTesting
  testImplementation libs.kotlin.compiler
  testImplementation libs.kotlin.test
  testImplementation libs.truth
}

//noinspection UnnecessaryQualifiedReference
tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompilationTask).configureEach {
  compilerOptions {
    // The flag is needed because we extend an interface that uses @JvmDefault and the Kotlin
    // compiler requires this flag when doing so.
    freeCompilerArgs.add("-Xjvm-default=all")
    freeCompilerArgs.add("-opt-in=org.jetbrains.kotlin.compiler.plugin.ExperimentalCompilerApi")
  }
}

if (!rootProject.ext.ci) {
  tasks.withType(Test).configureEach {
    maxParallelForks = Runtime.runtime.availableProcessors()
  }
}
